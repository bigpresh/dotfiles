#!/usr/bin/env perl

use strict;
use Getopt::Long;

# IDEA: add a -c flag that captures the matches and sums them up
# - you could just pipe to sort and uniq -c, but that would be cute.
#
my $first_lines = 0;

Getopt::Long::GetOptions(
    'first_lines=i' => \$first_lines,
);

my $patt = shift @ARGV;

my $patt_re = qr{$patt}
    or die "That doesn't look like a valid regexp";

my @json_data;
my $output_json;

# If we want to output the first $first_lines regardless, do so now...
while ($first_lines-- > 0) {
    print scalar <STDIN>;
}

while (my $line = <STDIN>) {
    if ($line =~ $patt_re) {
        if (keys %+) {
            # First, if we used named captures, we're after parseable output
            # so we'll turn them into JSON at the end
            push @json_data, {%+};
        } elsif (defined $1) {
            # Simple capture, output just the bit we wanted to capture
            print "$1\n";
        } else {
            # No captures, output the whole line
            print $line;
        }
    }
}

if (@json_data) {
    require JSON;
    print JSON::to_json(\@json_data);
}
